plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '2.0.1'
}

repositories {
    jcenter()
}

version '1.0-SNAPSHOT'
sourceCompatibility = 1.8
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.5.0'
def mainVerticleName = 'io.vertx.webproxy.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
    compile "io.vertx:vertx-core:$vertxVersion",
            "io.vertx:vertx-web:$vertxVersion",
            "io.vertx:vertx-web-templ-freemarker:$vertxVersion",
            "io.vertx:vertx-service-proxy:$vertxVersion",
            "com.github.rjeschke:txtmark:0.13",
            "io.vertx:vertx-jdbc-client:$vertxVersion",
            "org.hsqldb:hsqldb:2.3.4",
            "ch.qos.logback:logback-classic:1.2.3",
            "com.beust:jcommander:1.72"

    compileOnly "io.vertx:vertx-codegen:$vertxVersion"

    testCompile 'junit:junit:4.12',
                "io.vertx:vertx-unit:$vertxVersion",
                "com.github.tomakehurst:wiremock-standalone:2.6.0"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-AoutputDirectory=${project.projectDir}/src/main"
    ]
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}
